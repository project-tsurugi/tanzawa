plugins {
    id 'tanzawa.java-conventions'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
    // dependent projects
    implementation project(':tgsql:core')
    implementation project(':common:util')
    runtimeOnly "com.tsurugidb.tsubakuro:tsubakuro-ipc:${tsubakuroVersion}"
    runtimeOnly "com.tsurugidb.tsubakuro:tsubakuro-stream:${tsubakuroVersion}"

    implementation 'com.beust:jcommander:1.82'

    implementation 'org.jline:jline:3.21.0'
    implementation 'net.java.dev.jna:jna:5.12.1'
    implementation 'net.java.dev.jna:jna-platform:5.12.1'

    // peripherals
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
}

distributions {
    main {
        distributionBaseName = 'tgsql'
    }
    shadow {
        distributionBaseName = 'tgsql'
        distributionClassifier = 'shadow'
    }
}

application {
    applicationName = 'tgsql'
    mainClass = 'com.tsurugidb.console.cli.Main'
}

run {
    description 'run SQL script : -Dscript=</path/to/script> -Dconnection=<endpoint-uri> [-DlogLevel=<level>]'
    args = [ System.getProperty('script', '(unspecified)'), System.getProperty('connection', 'unspecified:unspecified') ]
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', System.getProperty('logLevel', 'INFO')
    jvmArgs = ['--add-opens=java.base/java.nio=ALL-UNNAMED']
}

shadowJar {
    archiveBaseName = 'tgsql'
    archiveClassifier = 'all'
    mergeServiceFiles()
}
