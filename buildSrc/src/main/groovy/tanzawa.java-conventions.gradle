plugins {
    id 'tanzawa.java-base'
    id 'checkstyle'

    id 'com.github.spotbugs'
    id 'com.github.hierynomus.license'
}

if (hasProperty('mavenLocal')) {
    repositories {
        mavenLocal()
    }
} else {
    repositories {
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            content {
                includeGroupByRegex 'com\\.tsurugidb.*'
            }
        }
    }
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 8, 'hours'
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
    def confDir = 'buildSrc/src/main/resources'
    configFile = rootProject.file("$confDir/checkstyle.xml")
    configProperties.suppressionFile = rootProject.file("$confDir/checkstyle-suppressions.xml")
    if ('strict' == findProperty('checkMode')) {
        maxWarnings = 0
    }
}

spotbugsMain {
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
    }
    excludeFilter = rootProject.file('buildSrc/src/main/resources/spotbugsExclude.xml')
}

spotbugsTest.enabled = false
checkstyleTest.enabled = false

javadoc.failOnError = false

jar {
    manifest.attributes (
        'Build-Timestamp': buildTimestamp,
        'Build-Revision' : buildRevision,
        'Build-Version'  : version,
        'Created-By'     : createdBy,
        'Build-Jdk'      : buildJdk,
        'Build-OS'       : buildOs,
    )
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        showStandardStreams true
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat 'full'
    }
}

task showTsubakuroManifest {
    doLast {
        def tsubakuroJar = configurations["compileClasspath"].filter {
            it.name.startsWith("tsubakuro-session")
        }
        def version = resources.text.fromArchiveEntry(tsubakuroJar, "META-INF/MANIFEST.MF")
        print(version.asString())
    }
}

task writeVersion(type: WriteProperties) {
    description 'generate version file to META-INF/tsurugidb/{project.name}.properties'
    inputs.property('Build-Revision', buildRevision)
    destinationFile = file("${project.buildDir}/generated/version/META-INF/tsurugidb/${project.qualifiedProjectName}.properties")
    properties (
        'Build-Timestamp': buildTimestamp,
        'Build-Revision' : buildRevision,
        'Build-Version'  : version,
        'Created-By'     : createdBy,
        'Build-Jdk'      : buildJdk,
        'Build-OS'       : buildOs,
    )
}

sourceSets.main.output.dir("${project.buildDir}/generated/version")
processResources.dependsOn writeVersion

tasks.withType(Tar) { task ->
    task.archiveExtension = 'tar.gz'
    task.compression = Compression.GZIP
}

license {
    def confDir = 'buildSrc/src/main/resources'
    header rootProject.file("$confDir/source-header.txt")
    mapping('java', 'SLASHSTAR_STYLE')
    include('**/*.java')
}
