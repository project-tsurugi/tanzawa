plugins {
    id 'java'
}

group = 'com.tsurugidb.tools'
version = '1.7.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

def qualifyProjectName(Project project) {
    if (project == project.rootProject) {
        // root project
        return project.name
    }
    // sub-projects
    return qualifyProjectName(project.parent) + "-" + project.name
}

ext {
    tsubakuroVersion = '1.7.0'
    isReleaseVersion = !version.endsWith("SNAPSHOT")

    buildTimestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ")
    buildRevision = System.getenv("GITHUB_SHA") ?: ""
    createdBy = "Gradle ${gradle.gradleVersion}"
    buildJdk = "${javaToolchains.launcherFor(java.toolchain).get().getMetadata().javaRuntimeVersion} ${javaToolchains.launcherFor(java.toolchain).get().getMetadata().vendor} ${javaToolchains.launcherFor(java.toolchain).get().getMetadata().jvmVersion}"
    buildOs = "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"

    qualifiedProjectName = qualifyProjectName(project)
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding 'UTF-8'
}

tasks.withType(Javadoc) { task ->
    task.options.encoding 'UTF-8'
}
