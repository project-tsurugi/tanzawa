plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'signing'
    id 'maven-publish'

    id 'tanzawa.java-conventions'
}

publishing {
    repositories {
        maven {
            name 'OSSRH'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username findProperty('st.ossrh.user') ?: System.getenv('ST_OSSRH_USER')
                password findProperty('st.ossrh.password') ?: System.getenv('ST_OSSRH_PASSWORD')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Tanzawa'
                description = 'Tsurugi SQL console engine is a core library of Tsurugi SQL console.'
                url = 'https://github.com/project-tsurugi/tanzawa'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'project-tsurugi'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/project-tsurugi/tanzawa.git'
                    developerConnection = 'scm:git:https://github.com/project-tsurugi/tanzawa.git'
                    url = 'https://github.com/project-tsurugi/tanzawa'
                }
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask(":${project.name}:publishMavenJavaPublicationToOSSRHRepository") }
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
